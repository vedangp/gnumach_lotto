/*
 * Copyright (c) 1993, 1994  Carl A. Waldspurger.
 * All Rights Reserved.
 * 
 * Permission to use, copy, modify and distribute this software and its
 * documentation is hereby granted, provided that both the copyright
 * notice and this permission notice appear in all copies of the
 * software, derivative works or modified versions, and any portions
 * thereof, and that both notices appear in supporting documentation.
 * 
 * CARL WALDSPURGER ALLOWS FREE USE OF THIS SOFTWARE IN ITS "AS IS"
 * CONDITION, BUT DISCLAIMS ANY LIABILITY OF ANY KIND FOR ANY DAMAGES
 * WHATSOEVER RESULTING FROM THE USE OF THIS SOFTWARE.
 * 
 * It is requested that users of this software return any improvements
 * they make and grant the rights to redistribute these changes to:
 * 
 * Carl Waldspurger			or   carl@lcs.mit.edu
 * MIT Laboratory for Computer Science
 * 545 Technology Square, Room 206
 * Cambridge, MA 02139
 *
 */

/*
 * Project:	Mach Lottery Scheduler (Kernel Modifications)
 * Module:	Lotto Definitions (for MIG-based IPC)
 * Author:	Carl Waldspurger
 * History:
 *	
 *	08-Jun-94	Added lotto_remove_ticket_from_currency() and
 *			lotto_compute{ticket,currency}_value().
 *	07-Jun-94	Added issuer_hint parameters to accelerate ticket
 *			lookups by id.
 *	03-Jun-94	Added lotto_currency_by_thread().
 *	21-Apr-94	Defined lotto_ipc_enable() and lotto_quantum_enable().
 *	19-Apr-94	Defined lotto_quantum_metrics().
 *	12-Apr-94	Removed lotto_move_ticket_to_thread().
 *	03-Apr-94	Added lotto_ipc_metrics().
 *	01-Feb-94	Added wins arg to lotto_currency_info().
 *	26-Jan-94	Added CountInOut tag to routines which have
 *			variable-length array out parameters.
 *	18-Jan-94	Defined lotto_currency_ids().
 *	12-Jan-94	Defined lotto_metrics() and lotto_currency_info().
 *	05-Jan-94	Removed id argument from lotto_create_currency().
 *	04-Jan-94	Added lotto_currency_by_task().
 *	20-Dec-93	Updated to ue new lotto id types.
 *	16-Dec-93	Added lotto_currency_by_name().
 *	28-Oct-93	Minor modifications, added KERNEL_USER.
 *	27-Oct-93	First version started.
 *
 */

/* imports */

#ifdef	KERNEL
simport <kern/compat_xxx_defs.h>;	/* for obsolete routines */
#endif	KERNEL

import <mach/mach_lotto_types.h>;

/* identification */

subsystem
#if	KERNEL_SERVER
	  KernelServer
#endif	KERNEL_SERVER
#if	KERNEL_USER
	  KernelUser
#endif	KERNEL_USER
		       mach_lotto 711000;

/* basic types */

#include <mach/std_types.defs>
#include <mach/mach_types.defs>

/* lotto types */

#include <mach/mach_lotto_types.defs>

/* lotto kernel routines */

routine lotto_create_ticket
  (	pset		: processor_set_t;
   	currency_id	: lotto_currency_id_t;
   	amount		: lotto_funds_t;
   out	ticket_id	: lotto_ticket_id_t);

routine lotto_create_currency
  (	pset		: processor_set_t;
   	name		: lotto_currency_name_t;
   out	currency_id	: lotto_currency_id_t);

routine lotto_destroy_ticket
  (pset			: processor_set_t;
   ticket_id		: lotto_ticket_id_t;
   issuer_hint		: lotto_currency_id_t);

routine lotto_destroy_currency
  (pset			: processor_set_t;
   currency_id		: lotto_currency_id_t;
   destroy_tickets	: boolean_t);

routine lotto_move_ticket_to_currency
  (pset			: processor_set_t;
   ticket_id		: lotto_ticket_id_t;
   issuer_hint		: lotto_currency_id_t;
   currency_id		: lotto_currency_id_t);

routine lotto_remove_ticket_from_currency
  (pset			: processor_set_t;
   ticket_id		: lotto_ticket_id_t;
   issuer_hint		: lotto_currency_id_t;
   currency_id		: lotto_currency_id_t);

routine lotto_currency_by_name
  (	pset		: processor_set_t;
   	name		: lotto_currency_name_t;
   out	currency_id	: lotto_currency_id_t);

routine lotto_currency_by_task
  (	pset		: processor_set_t;
   	task		: task_t;
   out	currency_id	: lotto_currency_id_t);

routine lotto_currency_by_thread
  (	pset		: processor_set_t;
   	task		: thread_t;
   out	currency_id	: lotto_currency_id_t);

routine lotto_metrics
  (	pset		: processor_set_t;
   out	metrics		: lotto_metrics_info);

routine lotto_currency_info
  (	pset		: processor_set_t;
	currency_id	: lotto_currency_id_t;
   out	wins		: unsigned;
   out	name		: lotto_currency_name_t;
   out	tickets		: lotto_ticket_info_array, CountInOut);

routine lotto_currency_ids
  (	pset		: processor_set_t;
   out  currency_ids	: lotto_currency_id_array_t, CountInOut);

routine lotto_compute_ticket_value
  (	pset		: processor_set_t;
	ticket_id	: lotto_ticket_id_t;
	issuer_hint	: lotto_currency_id_t;
   out	value 		: lotto_funds_t);

routine lotto_compute_currency_value
  (	pset		: processor_set_t;
	currency_id	: lotto_currency_id_t;
   out	value		: lotto_funds_t);

routine lotto_ipc_metrics
  (	pset		: processor_set_t;
   out	metrics		: lotto_ipc_metrics_info);

routine lotto_quantum_metrics
  (	pset		: processor_set_t;
   out	metrics		: lotto_quantum_metrics_t);

routine lotto_ipc_enable
  (pset			: processor_set_t;
   enable		: boolean_t);	

routine lotto_quantum_enable
  (pset			: processor_set_t;
   enable		: boolean_t);	

/* reserved */

skip;
skip;
skip;
skip;

